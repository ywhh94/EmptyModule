if (isDebug.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"
    //限定资源命名开头
//    resourcePrefix "news_"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (isDebug.toBoolean()) {
                manifest.srcFile 'src/main/debug_news/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                //各个模块下的这个包名好像不能重名，编译有问题
                java {
                    exclude 'debug_news/**'
                }
            }

            //资源管理,项目比较大，layout布局太多了。
            res.srcDirs =
                    [
                            'src/main/res/main_layout',//activity的layout
                            'src/main/res/item_layout',//item的layout
                            'src/main/res/views_layout',//自定义view的layout
                            'src/main/res'
                    ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'

    implementation project(':baselib')

//    api project(path: ':baselib')
}
